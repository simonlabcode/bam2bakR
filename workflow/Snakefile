configfile: "config/config.yaml"

CPU_num = config['cpus']

SAMP_NAMES = list(config['samples'].keys())

CTL_NAMES = list(config['control_samples'])

nctl = len(CTL_NAMES)

# MAKE_TRACKS = config['make_tracks']
#
# COUNT = ["0", "1", "2", "3", "4", "5"]


def get_input_bams(wildcards):
    return config["samples"][wildcards.sample]

rule all:
    input:
        "results/cB/cB.csv.gz",
        expand("results/tracks/{S_ID}_success.txt", S_ID=SAMP_NAMES)
        #expand("results/counts/{S_ID}_counts.csv.gz", S_ID=SAMP_NAMES)


rule sort_filter:
    input:
        get_input_bams
    output:
        "results/sf_reads/{sample}.s.sam"
    log:
        "logs/sort_filter/{sample}.log"
    threads: CPU_num
    shell:
        "workflow/scripts/sort_filter.sh {config[cpus]} {wildcards.sample} {input} {output} {config[FORMAT]}"

rule htseq_cnt:
    input:
        "results/sf_reads/{sample}.s.sam"
    output:
        "results/bams/{sample}_tl.bam"
    log:
        "logs/htseq_cnt/{sample}.log"
    threads: CPU_num
    shell:
        "workflow/scripts/htseq.sh {config[cpus]} {wildcards.sample} {input} {output} {config[annotation]} {config[mutcnt]}"

rule call_snps:
    input:
        expand("results/bams/{ctl}_tl.bam", ctl = CTL_NAMES)
    params:
        nsamps = nctl
    output:
        "results/snps/snp.txt"
    log:
        "logs/call_snps/ctl_samps.log"
    threads: CPU_num
    shell:
        "workflow/scripts/call_snps.sh {config[cpus]} {params.nsamps} {output} {config[genome_fasta]} {input}"

rule cnt_muts:
    input:
        "results/bams/{sample}_tl.bam",
        "results/snps/snp.txt"
    output:
        "results/counts/{sample}_counts.csv.gz"
    log:
        "logs/fragment_bam/{sample}.log"
    threads: CPU_num
    shell:
        "workflow/scripts/fragment.sh {config[cpus]} {wildcards.sample} {input} {output} {config[awkscript]} {config[fragment_size]} {config[minqual]} {config[mut_tracks]} {config[mutcall]} {config[FORMAT]}"

rule maketdf:
    input:
        "results/counts/{sample}_counts.csv.gz",
        "results/bams/{sample}_tl.bam"
    output:
        "results/tracks/{sample}_success.txt"
    threads: CPU_num
    shell:
        "workflow/scripts/tracks.sh {config[cpus]} {wildcards.sample} {input} {config[mut_tracks]} {output} {config[genome_fasta]} {config[WSL]}"

rule makecB:
    input:
        expand("results/counts/{sample}_counts.csv.gz", sample=config["samples"])
    output:
        "results/cB/cB.csv.gz"
    log:
        "logs/makecB/master.log"
    threads: CPU_num
    shell:
        "workflow/scripts/master.sh {config[cpus]} {output} {config[keepcols]} {config[mut_tracks]}"
